buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.3.0"
    }
}

allprojects {
    group 'com.trendmicro.course.unittesting'
    version '0.0.1'
}

subprojects { subproject ->

    apply plugin: 'java'
    apply plugin: 'info.solidsoft.pitest'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.encoding = 'UTF-8'
        }
        // https://docs.gradle.org/current/dsl/org.gradle.api.tasks.testing.Test.html
        tasks.withType(Test) {
            testLogging.showStandardStreams = true
            beforeTest { descriptor ->
                logger.lifecycle("Test: [" + descriptor.className + "] . " + descriptor.name + "() " + "[Start]")
            }
            // https://docs.gradle.org/current/javadoc/org/gradle/api/tasks/testing/TestResult.html
            afterTest { descriptor, result ->
                logger.lifecycle("Test: [" + descriptor.className + "] . " + descriptor.name + "() " + "[Done]"
                        + ", result = " + result.resultType)
                if (result.exception) {
                    result.exception.printStackTrace()
                }
            }
        }
    }

}